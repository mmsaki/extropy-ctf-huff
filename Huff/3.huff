/// # Challenge 3: abi.encodePacked
/// 
/// Write a function that unpacks our data that was packed using abi.encodePacked(a, b, c). 
/// Where a is type uint8, b is type bool and c is type bytes1.
///
/// ## Directives
/// 1. Get value offset packed in calldata
///   > You can also solve using packed value in memory
/// 2. Get  uint8 a, shift right by 0xf8 (8 * 31 bytes) `0xff__ --> 0x__ff`
/// 3. Get   bool b, shift right by 0xf0 (8 * 30 bytes) `0x_ff_ --> 0x__ff`
/// 4. Get bytes1 c, shift  left by 0x10 (8 *  2 bytes) `0x__ff --> 0xff__`
///
/// ### Panics
/// - Reverts if selector is not equal to `0xce0b1238`

/// ### Calldata layout
/// 
/// `selector . size . length . bytes`
///  
/// | name     | size (bytes) | offset |
/// | -------- | ------------ | ------ |
/// | selector | 4            | 0x00   |
/// | size     | 32           | 0x04   |
/// | length   | 32           | 0x24   |
/// | bytes    | 3            | 0x44   |
#define function solution(bytes memory packed) pure returns (uint8 a, bool b, bytes1 c)

#define macro MAIN() = takes(0) returns(0) {
    push0                 // [offset]
    calldataload 0xe0 shr // [selector]
    __FUNC_SIG(solution)  // [solution_selc, selector]
    eq solution jumpi     // [solution_dest, truthy]
    push0 push0 revert    // []
      
    solution:
      0x44          // [offset]
      calldataload  // [bytes] ? value == 0xaabbcc....
      dup1 dup1     // [value, value, value]
      
      // uint8 a
      0xf8 shr      // [a, value, value]
      push0 mstore  // [value, value]

      // bool b
      0xf0 shr      // [0xbbcc, value]
      0x00ff and    // [b, value]
      0x20 mstore   // [value]

      // bytes1 c
      0x10 shl      // [c]
      0x40 mstore   // []

      0x60          // [mem_len]       
      push0         // [mem_ptr, mem_len]
      return        // []
}